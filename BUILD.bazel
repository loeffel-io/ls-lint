load("@bazel_gazelle//:def.bzl", "gazelle")
load("@io_bazel_rules_go//go:def.bzl", "TOOLS_NOGO", "nogo")

# gazelle:prefix github.com/loeffel-io/ls-lint/v2
# gazelle:exclude vendor
# gazelle:exclude .idea
gazelle(name = "gazelle")

gazelle(
    name = "gazelle_update_repos",
    args = [
        "--from_file=go.mod",
        "--to_macro=repositories.bzl%go_repositories",
        "--prune",
    ],
    command = "update-repos",
)

gazelle(
    name = "gazelle_fix_diff",
    command = "fix",
    extra_args = ["--mode=diff"],
)

config_setting(
    name = "darwin_arm64",
    constraint_values = [
        "@platforms//os:osx",
        "@platforms//cpu:arm64",
    ],
)

nogo(
    name = "nogo",
    config = ":nogo.json",
    visibility = ["//visibility:public"],
    deps = TOOLS_NOGO + [],
)

exports_files(
    srcs = [
        "LICENSE",
        "README.md",
    ],
    visibility = ["//visibility:public"],
)

alias(
    name = "gh",
    actual = select({
        ":darwin_arm64": "@com_github_cli_cli_darwin_arm64//:bin/gh",
        "//conditions:default": "@com_github_cli_cli_linux_amd64//:bin/gh",
    }),
    visibility = ["//visibility:public"],
)

alias(
    name = "coreutils",
    actual = select({
        ":darwin_arm64": "@com_github_uutils_coreutils_darwin_arm64//:coreutils",
        "//conditions:default": "@com_github_uutils_coreutils_linux_amd64//:coreutils",
    }),
    visibility = ["//visibility:public"],
)
